plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven'
}

group='com.github.aesteve'
version='0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	// vertx
	compile 'io.vertx:vertx-core:3.0.0'
	compile 'io.vertx:vertx-web:3.0.0'
    compile 'io.vertx:vertx-auth-common:3.0.0'
    compile 'io.vertx:vertx-auth-shiro:3.0.0'
	compile 'io.vertx:vertx-auth-jwt:3.0.0'
    compile 'io.vertx:vertx-auth-jdbc:3.0.0'
    compile 'io.vertx:vertx-jdbc-client:3.0.0'

	compile 'io.vertx:vertx-service-proxy:3.0.0'
	compile 'io.vertx:vertx-codegen:3.0.0'

	// reflection
	compile 'org.reflections:reflections:0.9.9'
	compile 'commons-beanutils:commons-beanutils:1.9.2'
	
	// JSON api
	compile 'io.fastjson:boon:0.32'
	
	// tests
    testCompile 'com.github.jknack:handlebars:1.3.0'
	testCompile 'io.vertx:vertx-unit:3.0.0'
	testCompile 'junit:junit:4.12'
}


/* Service Proxy related */
def generatedSrcPath="$buildDir/generated-src"
def generatedSrcDir = file(generatedSrcPath)

sourceSets {
	test {
		java.srcDirs += generatedSrcDir
		output.dir(builtBy: 'generateServiceProxy', generatedSrcPath)
	}
}

task generateServiceProxy(type: JavaCompile, description: 'Generates EBServiceProxies') {
	source = [sourceSets.test.java, sourceSets.main.java]
	classpath = configurations.testCompile
	destinationDir = generatedSrcDir
	options.compilerArgs = [
		"-proc:only",
		"-processor", "io.vertx.codegen.CodeGenProcessor",
		"-AoutputDirectory=$generatedSrcPath"
	]
}

compileTestJava.dependsOn generateServiceProxy

clean {
	delete += generatedSrcDir
}




test.outputs.upToDateWhen { false }

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}
